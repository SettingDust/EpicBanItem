import team.ebi.gradle.TaskOreDeploy

plugins {
    id "com.github.johnrengelman.shadow" version "2.0.4"
    id "ninja.miserable.blossom" version "1.0.1"
    id "signing"
    id "java"
    id "idea"
}

def pluginVersion = {
    def command = "git describe --abbrev=0 --tags"
    def version = command.execute().text.trim()
    println(version)
    return version.length() > 0 ? version.substring(1) : ""
}

def buildNumber = {
    def command = "git rev-list --count --first-parent HEAD"
    println(command.execute().text.trim())
    return command.execute().text.trim().toInteger() - 1
}

version = "${pluginVersion()}-b${buildNumber()}"

defaultTasks "clean", "build"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        name = "sponge"
        url = "https://repo.spongepowered.org/maven"
    }
}

dependencies {
    compile "org.spongepowered:spongeapi:$spongeVersion"
}

jar {
    classifier = "pure"
}

shadowJar {
    classifier = null

    build.dependsOn += it

    configurations = [] // no dependency now
}

task oreDeploy(type: TaskOreDeploy) {
    pluginId = project.ext.id
    apiKey = System.getProperty("ore.key")
    setPluginInfo(
            recommended: true,
            createForumPost: true,
//            description: "v0.4.0",
            tags: [Channel: "PreRelease"]
    )
    jarTask = "shadowJar"
}

blossom {
    replaceToken '@version@', pluginVersion()
}

signing {
    sign jar, shadowJar each {
        it.enabled project.findProperty("signing.password") as boolean
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:all" // << "-Werror"
}
